#include "stdio"
    >+++++[-<+++++++>]                    <.> #
    >>+++[-<+++++>]<[-<+++++++>]          <.> i
    >++++++++++[-<+++++++++++>]           <.> n
    >+++++++++[-<+++++++++++>]            <.> c
    >>++++[-<+++>]<[-<+++++++++>]         <.> l
    >++++++++++[-<++++++++++++>]<--->     <.> u
    >++++++++++[-<++++++++++>]            <.> d
    >++++++++++[-<++++++++++>]<+>         <.> e
    >++++[-<++++++++>]                    <.> (space)
    >+++++[-<+++++++>]<->                 <.> "
    >++++++++++[-<++++++++++++>]<----->   <.> s
    >++++++++++[-<++++++++++++>]<---->    <.> t
    >++++++++++[-<++++++++++>]            <.> d
    >>+++[-<+++++>]<[-<+++++++>]          <.> i
    >++++++++++[-<+++++++++++>]<+>        <.> o
    >++++++++++[-<+++++>]<---->           <.> (dot)
    >>+++[-<+++++>]<[-<+++++++>]<->       <.> h
    >+++++[-<+++++++>]<->                 <.> "
    ++++++++++>                           <.> (\n)
#include <string>
    >+++++[-<+++++++>]                    <.> #
    >>+++[-<+++++>]<[-<+++++++>]          <.> i
    >++++++++++[-<+++++++++++>]           <.> n
    >+++++++++[-<+++++++++++>]            <.> c
    >>++++[-<+++>]<[-<+++++++++>]         <.> l
    >++++++++++[-<++++++++++++>]<--->     <.> u
    >++++++++++[-<++++++++++>]            <.> d
    >++++++++++[-<++++++++++>]<+>         <.> e
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<++++++>]                <.> (lt)
    >++++++++++[-<++++++++++++>]<----->   <.> s
    >++++++++++[-<++++++++++++>]<---->    <.> t
    >++++++++++[-<++++++++++++>]<------>  <.> r
    >>+++[-<+++++>]<[-<+++++++>]          <.> i
    >++++++++++[-<+++++++++++>]           <.> n
    >++++++++++[-<++++++++++>]<+++>       <.> g
    >++++++++++[-<+++++>]<---->           <.> (dot)
    >>+++[-<+++++>]<[-<+++++++>]<->       <.> h
    >++++++++++[-<++++++>]<++>            <.> (gt)
    ++++++++++>                           <.> (\n)
++++++++++>                               <.> (\n)
#define ds 1048576
    >+++++[-<+++++++>]                    <.> #
    >++++++++++[-<++++++++++>]            <.> d
    >++++++++++[-<++++++++++>]<+>         <.> e
    >++++++++++[-<++++++++++>]<++>        <.> f
    >>+++[-<+++++>]<[-<+++++++>]          <.> i
    >++++++++++[-<+++++++++++>]           <.> n
    >++++++++++[-<++++++++++>]<+>         <.> e
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<++++++++++>]            <.> d
    >++++++++++[-<++++++++++++>]<----->   <.> s
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<+++++>]<->              <.> 1
    >++++++++++[-<+++++>]<-->             <.> 0
    >++++++++++[-<+++++>]<++>             <.> 4
    >++++++++++[-<+++++>]<++++++>         <.> 8
    >++++++++++[-<+++++>]<+++>            <.> 5
    >++++++++++[-<+++++>]<+++++>          <.> 7
    >++++++++++[-<+++++>]<++++>           <.> 6
    ++++++++++>                           <.> (\n)
++++++++++>                               <.> (\n)
int main(void) {
    >>+++[-<+++++>]<[-<+++++++>]          <.> i
    >++++++++++[-<+++++++++++>]           <.> n
    >++++++++++[-<++++++++++++>]<---->    <.> t
    >++++[-<++++++++>]                    <.> (space)
    >>++++[-<+++>]<[-<+++++++++>]<+>      <.> m
    >++++++++++[-<++++++++++>]<--->       <.> a
    >>+++[-<+++++>]<[-<+++++++>]          <.> i
    >++++++++++[-<+++++++++++>]           <.> n
    >++++++++++[-<++++>]                  <.> (
    >++++++++++[-<++++++++++++>]<-->      <.> v
    >++++++++++[-<+++++++++++>]<+>        <.> o
    >>+++[-<+++++>]<[-<+++++++>]          <.> i
    >++++++++++[-<++++++++++>]            <.> d
    >++++++++++[-<++++>]<+>               <.> )
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<++++++++++++>]<+++>     <.> {
    ++++++++++>                           <.> (\n)
char d(os)ds(cs);
    >+++++++++[-<+++++++++++>]            <.> c
    >>+++[-<+++++>]<[-<+++++++>]<->       <.> h
    >++++++++++[-<++++++++++>]<--->       <.> a
    >++++++++++[-<++++++++++++>]<------>  <.> r
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<++++++++++>]            <.> d
    >++++++++++[-<+++++++++>]<+>          <.> (os)
    >++++++++++[-<++++++++++>]            <.> d
    >++++++++++[-<++++++++++++>]<----->   <.> s
    >++++++++++[-<+++++++++>]<+++>        <.> (cs)
    >++++++++++[-<++++++>]<->             <.> ;
    ++++++++++>                           <.> (\n)
memset(d(comma) 0(comma) ds);
    >>++++[-<+++>]<[-<+++++++++>]<+>      <.> m
    >++++++++++[-<++++++++++>]<+>         <.> e
    >>++++[-<+++>]<[-<+++++++++>]<+>      <.> m
    >++++++++++[-<++++++++++++>]<----->   <.> s
    >++++++++++[-<++++++++++>]<+>         <.> e
    >++++++++++[-<++++++++++++>]<---->    <.> t
    >++++++++++[-<++++>]                  <.> (
    >++++++++++[-<++++++++++>]            <.> d
    >++++++++++[-<++++>]<++++>            <.> (comma)
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<+++++>]<-->             <.> 0
    >++++++++++[-<++++>]<++++>            <.> (comma)
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<++++++++++>]            <.> d
    >++++++++++[-<++++++++++++>]<----->   <.> s
    >++++++++++[-<++++>]<+>               <.> )
    >++++++++++[-<++++++>]<->             <.> ;
    ++++++++++>                           <.> (\n)
int p = 0;
    >>+++[-<+++++>]<[-<+++++++>]          <.> i
    >++++++++++[-<+++++++++++>]           <.> n
    >++++++++++[-<++++++++++++>]<---->    <.> t
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<+++++++++++>]<++>       <.> p
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<++++++>]<+>             <.> =
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<+++++>]<-->             <.> 0
    >++++++++++[-<++++++>]<->             <.> ;
    ++++++++++>                           <.> (\n)
|
|
TEMPLATES
|
>                                         (\0)
|
d(os)p(cs)(plus)(plus);                   120 
    >++++++++++[-<++++++++++>]            d
    >++++++++++[-<+++++++++>]<+>          (os)
    >++++++++++[-<+++++++++++>]<++>       p
    >++++++++++[-<+++++++++>]<+++>        (cs)
    >++++++++++[-<++++>]<+++>             (plus)
    >++++++++++[-<++++>]<+++>             (plus)
    >++++++++++[-<++++++>]<->             ;
    ++++++++++>                           (\n)
    >                                     (\0)
|
d(os)p(cs)(sub)(sub);                     112
    >++++++++++[-<++++++++++>]            d
    >++++++++++[-<+++++++++>]<+>          (os)
    >++++++++++[-<+++++++++++>]<++>       p
    >++++++++++[-<+++++++++>]<+++>        (cs)
    >++++++++++[-<++++>]<+++++>           (sub)
    >++++++++++[-<++++>]<+++++>           (sub)
    >++++++++++[-<++++++>]<->             ;
    ++++++++++>                           (\n)
    >                                     (\0)
|
if ((plus)(plus)p (gt)= ds) p = 0;        104
    >>+++[-<+++++>]<[-<+++++++>]          i
    >++++++++++[-<++++++++++>]<++>        f
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++>]                  (
    >++++++++++[-<++++>]<+++>             (plus)
    >++++++++++[-<++++>]<+++>             (plus)
    >++++++++++[-<+++++++++++>]<++>       p
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++++>]<++>            (gt)
    >++++++++++[-<++++++>]<+>             =
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++++++++>]            d
    >++++++++++[-<++++++++++++>]<----->   s
    >++++++++++[-<++++>]<+>               )
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<+++++++++++>]<++>       p
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++++>]<+>             =
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<+++++>]<-->             0
    >++++++++++[-<++++++>]<->             ;
    ++++++++++>                           (\n)
    >                                     (\0)
|
if ((sub)(sub)p (lt) 0) p = ds (sub) 1;   82
    >>+++[-<+++++>]<[-<+++++++>]          i
    >++++++++++[-<++++++++++>]<++>        f
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++>]                  (
    >++++++++++[-<++++>]<+++++>           (sub)
    >++++++++++[-<++++>]<+++++>           (sub)
    >++++++++++[-<+++++++++++>]<++>       p
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++++>]                (lt)
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<+++++>]<-->             0
    >++++++++++[-<++++>]<+>               )
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<+++++++++++>]<++>       p
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++++>]<+>             =
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++++++++>]            d
    >++++++++++[-<++++++++++++>]<----->   s
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++>]<+++++>           (sub)
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<+++++>]<->              1
    >++++++++++[-<++++++>]<->             ;
    ++++++++++>                           (\n)
    >                                     (\0)
|
printf("%c"(comma) d(os)p(cs));           57
    >++++++++++[-<+++++++++++>]<++>       p
    >++++++++++[-<++++++++++++>]<------>  r
    >>+++[-<+++++>]<[-<+++++++>]          i
    >++++++++++[-<+++++++++++>]           n
    >++++++++++[-<++++++++++++>]<---->    t
    >++++++++++[-<++++++++++>]<++>        f
    >++++++++++[-<++++>]                  (
    >+++++[-<+++++++>]<->                 "
    >+++++[-<+++++++>]<++>                %
    >+++++++++[-<+++++++++++>]            c
    >+++++[-<+++++++>]<->                 "
    >++++++++++[-<++++>]<++++>            (comma)
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++++++++>]            d
    >++++++++++[-<+++++++++>]<+>          (os)
    >++++++++++[-<+++++++++++>]<++>       p
    >++++++++++[-<+++++++++>]<+++>        (cs)
    >++++++++++[-<++++>]<+>               )
    >++++++++++[-<++++++>]<->             ;
    ++++++++++>                           (\n)
    >                                     (\0)
|
scanf("%c"(comma) &d(os)p(cs));           38
    >++++++++++[-<++++++++++++>]<----->   s
    >+++++++++[-<+++++++++++>]            c
    >++++++++++[-<++++++++++>]<--->       a
    >++++++++++[-<+++++++++++>]           n
    >++++++++++[-<++++++++++>]<++>        f
    >++++++++++[-<++++>]                  (
    >+++++[-<+++++++>]<->                 "
    >+++++[-<+++++++>]<++>                %
    >+++++++++[-<+++++++++++>]            c
    >+++++[-<+++++++>]<->                 "
    >++++++++++[-<++++>]<++++>            (comma)
    >++++[-<++++++++>]                    (space)
    >+++++[-<+++++++>]<+++>               &
    >++++++++++[-<++++++++++>]            d
    >++++++++++[-<+++++++++>]<+>          (os)
    >++++++++++[-<+++++++++++>]<++>       p
    >++++++++++[-<+++++++++>]<+++>        (cs)
    >++++++++++[-<++++>]<+>               )
    >++++++++++[-<++++++>]<->             ;
    ++++++++++>                           (\n)
    >                                     (\0)
|
while (d(os)p(cs)) {                      18
    >++++++++++[-<++++++++++++>]<->       w
    >>+++[-<+++++>]<[-<+++++++>]<->       h
    >>+++[-<+++++>]<[-<+++++++>]          i
    >>++++[-<+++>]<[-<+++++++++>]         l
    >++++++++++[-<++++++++++>]<+>         e
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++>]                  (
    >++++++++++[-<++++++++++>]            d
    >++++++++++[-<+++++++++>]<+>          (os)
    >++++++++++[-<+++++++++++>]<++>       p
    >++++++++++[-<+++++++++>]<+++>        (cs)
    >++++++++++[-<++++>]<+>               )
    >++++[-<++++++++>]                    (space)
    >++++++++++[-<++++++++++++>]<+++>     {
    ++++++++++>                           (\n)
    >                                     (\0)
|
}                                         3
    >++++++++++[-<++++++++++++>]<+++++>   }
    ++++++++++>                           (\n)
    >                                     (\0)
|
|
    ,                                     input
    [->+>+<<]>>[-<<+>>]                   input_copy
    ++++++++++>                           (\n)
    <[-<->]<                              input minus (\n)
    [                                     while input != (\n)
|
        (plus)
        [-]<[->+>+<<]>>[-<<+>>]           input_copy
        >++++++++++[-<++++>]<+++>         (plus)
        <[-<->]<                          input minus (plus)
        >>>>+<<<<                         is_plus = 1
        [[-]>>>>-<<<<][-]<                if input != (plus): is_plus = 0
        [->+>+<<]>>[-<<+>>]               input_copy
        >++++++++++[-<++++>]<+++>         (plus)
        <[-<->]<                          input minus (plus)
        >>>>[                             if is_plus
            -                             is_plus = 0
            <<<< < <<
            [<]< [<]<
            [<]< [<]<
            [<]< [<]<
            [<]< [<]
            >[.>]>
            [>]> [>]>
            [>]> [>]>
            [>]> [>]>
            [>]
            > > >>>>
        ]<<<<
|
        (sub)
        [-]<[->+>+<<]>>[-<<+>>]           input_copy
        >++++++++++[-<++++>]<+++++>       (sub)
        <[-<->]<                          input minus (sub)
        >>>>+<<<<                         is_sub = 1
        [[-]>>>>-<<<<][-]<                if input != (sub): is_sub = 0
        [->+>+<<]>>[-<<+>>]               input_copy
        >++++++++++[-<++++>]<+++++>       (sub)
        <[-<->]<                          input minus (sub)
        >>>>[                             if is_sub
            -                             is_sub = 0
            <<<< < <<
            [<]< [<]<
            [<]< [<]<
            [<]< [<]<
            [<]
            >[.>]>
            [>]> [>]>
            [>]> [>]>
            [>]> [>]
            > > >>>>
        ]<<<<
|
        (gt)
        [-]<[->+>+<<]>>[-<<+>>]           input_copy
        >++++++++++[-<++++++>]<++>        (gt)
        <[-<->]<                          input minus (gt)
        >>>>+<<<<                         is_gt = 1
        [[-]>>>>-<<<<][-]<                if input != (gt): is_gt = 0
        [->+>+<<]>>[-<<+>>]               input_copy
        >++++++++++[-<++++++>]<++>        (gt)
        <[-<->]<                          input minus (gt)
        >>>>[                             if is_gt
            -                             is_gt = 0
            <<<< < <<
            [<]< [<]<
            [<]< [<]<
            [<]< [<]
            >[.>]>
            [>]> [>]>
            [>]> [>]>
            [>]
            > > >>>>
        ]<<<<
|
        (lt)
        [-]<[->+>+<<]>>[-<<+>>]           input_copy
        >++++++++++[-<++++++>]            (lt)
        <[-<->]<                          input minus (lt)
        >>>>+<<<<                         is_lt = 1
        [[-]>>>>-<<<<][-]<                if input != (lt): is_lt = 0
        [->+>+<<]>>[-<<+>>]               input_copy
        >++++++++++[-<++++++>]            (lt)
        <[-<->]<                          input minus (lt)
        >>>>[                             if is_lt
            -                             is_lt = 0
            <<<< < <<
            [<]< [<]<
            [<]< [<]<
            [<]
            >[.>]>
            [>]> [>]>
            [>]> [>]
            > > >>>>
        ]<<<<
|
        (dot)
        [-]<[->+>+<<]>>[-<<+>>]           input_copy
        >++++++++++[-<+++++>]<---->       (dot)
        <[-<->]<                          input minus (dot)
        >>>>+<<<<                         is_dot = 1
        [[-]>>>>-<<<<][-]<                if input != (dot): is_dot = 0
        [->+>+<<]>>[-<<+>>]               input_copy
        >++++++++++[-<+++++>]<---->       (dot)
        <[-<->]<                          input minus (dot)
        >>>>[                             if is_dot
            -                             is_dot = 0
            <<<< < <<
            [<]< [<]<
            [<]< [<]
            >[.>]>
            [>]> [>]>
            [>]
            > > >>>>
        ]<<<<
|
        (comma)
        [-]<[->+>+<<]>>[-<<+>>]           input_copy
        >++++++++++[-<++++>]<++++>        (comma)
        <[-<->]<                          input minus (comma)
        >>>>+<<<<                         is_comma = 1
        [[-]>>>>-<<<<][-]<                if input != (comma): is_comma = 0
        [->+>+<<]>>[-<<+>>]               input_copy
        >++++++++++[-<++++>]<++++>        (comma)
        <[-<->]<                          input minus (comma)
        >>>>[                             if is_comma
            -                             is_comma = 0
            <<<< < <<
            [<]< [<]<
            [<]
            >[.>]>
            [>]> [>]
            > > >>>>
        ]<<<<
|
        (os)
        [-]<[->+>+<<]>>[-<<+>>]           input_copy
        >++++++++++[-<+++++++++>]<+>      (os)
        <[-<->]<                          input minus (os)
        >>>>+<<<<                         is_os = 1
        [[-]>>>>-<<<<][-]<                if input != (os): is_os = 0
        [->+>+<<]>>[-<<+>>]               input_copy
        >++++++++++[-<+++++++++>]<+>      (os)
        <[-<->]<                          input minus (os)
        >>>>[                             if is_os
            -                             is_os = 0
            <<<< < <<
            [<]< [<]
            >[.>]>
            [>]
            > > >>>>
        ]<<<<
|
        (cs)
        [-]<[->+>+<<]>>[-<<+>>]           input_copy
        >++++++++++[-<+++++++++>]<+++>    (cs)
        <[-<->]<                          input minus (cs)
        >>>>+<<<<                         is_cs = 1
        [[-]>>>>-<<<<][-]<                if input != (cs): is_cs = 0
        [->+>+<<]>>[-<<+>>]               input_copy
        >++++++++++[-<+++++++++>]<+++>    (cs)
        <[-<->]<                          input minus (cs)
        >>>>[                             if is_cs
            -                             is_cs = 0
            <<<< < <<
            [<]
            >[.>]
            > > >>>>
        ]<<<<
|
        [-]<,                             input
        [->+>+<<]>>[-<<+>>]               input_copy
        ++++++++++>                       (\n)
        <[-<->]<                          input minus (\n)
        >>+<<                             is_eol = 1
        [[-]>>-<<][-]<                    if input != (\n): is_eol = 0
        [->+>+<<]>>[-<<+>>]               input_copy
        ++++++++++>                       (\n)
        <[-<->]<                          input minus (\n)
        >>[                               if is_eol
            -                             is_eol = 0
            <<[-]<,                       input
            [->+>+<<]>>[-<<+>>]           input_copy
            ++++++++++>                   (\n)
            <[-<->]<                      input minus (\n)
            >>
        ]<<
    ]
    >>>>>>>>>>>
|
|
return 0;
    >++++++++++[-<++++++++++++>]<------>  <.> r
    >++++++++++[-<++++++++++>]<+>         <.> e
    >++++++++++[-<++++++++++++>]<---->    <.> t
    >++++++++++[-<++++++++++++>]<--->     <.> u
    >++++++++++[-<++++++++++++>]<------>  <.> r
    >++++++++++[-<+++++++++++>]           <.> n
    >++++[-<++++++++>]                    <.> (space)
    >++++++++++[-<+++++>]<-->             <.> 0
    >++++++++++[-<++++++>]<->             <.> ;
    ++++++++++>                           <.> (\n)
}
    >++++++++++[-<++++++++++++>]<+++++>   <.> }
++++++++++>                               <.> (\n)

